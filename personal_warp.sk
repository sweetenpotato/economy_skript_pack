on join:
    if {warp_amount.%uuid of player%} is not set:
        set {warp_amount.%uuid of player%} to 0

command /워프 [<text>] [<text>]:
    trigger:
        if arg 1 is "생성":
            if {warp_amount.%uuid of player%} <= 2:
                if arg 2 is set:
                    if {warp.%uuid of player%::%arg 2%} is set:
                        message "[Warp Skript] 이미 생성된 워프입니다! 생성이 불가능 합니다!"
                    else:
                        set {warp.%uuid of player%::%arg 2%} to location of player
                        add 1 to {warp_amount.%uuid of player%}
                        message "[Warp Skript] 워프 %arg 2%가 생성되었습니다!"
                else:
                    message "[Warp Skript] 유효한 워프 이름을 입력해 주세요!"
            else:
                message "[Warp Skript] 3개 이상의 워프는 등록하실 수 없습니다!"
        else if arg 1 is "이동":
            if {warp.%uuid of player%::%arg 2%} is set:
                teleport player to {warp.%uuid of player%::%arg 2%}
                message "[Warp Skript] 워프 %arg 2%로 이동했습니다!"
            else:
                message "[Warp Skript] 그런 이름을 가진 워프는 존재하지 않습니다!"
        else if arg 1 is "삭제":
            if {warp.%uuid of player%::%arg 2%} is set:
                clear {warp.%uuid of player%::%arg 2%}
                subtract 1 from {warp_amount.%uuid of player%}
                message "[Warp Skript] 워프 %arg 2%가 삭제되었습니다."
            else:
                message "[Warp Skript] 그런 이름을 가진 워프는 존재하지 않습니다!"
        else:
            message "[Warp Skript] 유효한 명령어를 입력해 주세요!"
